pkgname=niri-no-systemd-git
_pkgname=niri
pkgver=25.08
pkgrel=1
pkgdesc="Scrollable-tiling Wayland compositor (no systemd services)"
arch=(x86_64 aarch64)
url="https://github.com/YaLTeR/$_pkgname"
license=(GPL-3.0-or-later)
depends=(cairo glib2 libdisplay-info libinput libpipewire libxkbcommon mesa pango pixman seatd)
makedepends=(clang rust git makepkg-git-lfs-proto)
[[ -n ${_sccache} ]] && makedepends+=(sccache)
optdepends=(
   'fuzzel: application launcher similar to rofi drun mode'
   'waybar: highly customizable Wayland bar'
   'alacritty: a cross-platform OpenGL terminal emulator'
   'mako: notification daemon for Wayland'
   'swaybg: wallpaper tool for Wayland compositors'
   'swaylock: screen locker for Wayland'
   'xdg-desktop-portal-gtk: implements most of the basic functionality'
   'xdg-desktop-portal-gnome: screencasting support'
   'gnome-keyring: implements the secret portal, for certain apps to work'
   'polkit-gnome: when apps need to ask for root permissions'
)
provides=($_pkgname)
conflicts=($_pkgname $_pkgname-bin $_pkgname-git)
options=(!debug !lto !strip)
source=($_pkgname::git-lfs+$url.git)
b2sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd $_pkgname
  export CARGO_HOME=${srcdir}/$_pkgname/.cargo
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_pkgname
  export RUSTFLAGS="--remap-path-prefix=${srcdir}=/"
  [[ -n ${_sccache} ]] && export RUSTC_WRAPPER=sccache
  export CARGO_HOME=${srcdir}/$_pkgname/.cargo
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --no-default-features --features="dbus,xdp-gnome-screencast"
}

package() {
  cd $_pkgname
  install -Dm755 target/release/$_pkgname                -t "${pkgdir}/usr/bin/"
  install -Dm755 resources/$_pkgname-session             -t "${pkgdir}/usr/bin/"
  install -Dm644 resources/default-config.kdl             -t "${pkgdir}/usr/share/doc/$_pkgname"
  install -Dm644 resources/$_pkgname.desktop             -t "${pkgdir}/usr/share/wayland-sessions/"
  install -Dm644 resources/$_pkgname-portals.conf        -t "${pkgdir}/usr/share/xdg-desktop-portal/"
}
